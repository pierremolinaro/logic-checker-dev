#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#    AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

class @graphvizDefinition : @definition {
  @lstring mBlockName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#    SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension logicchecker_syntax {

  #·····················································································································

  rule <definition> !@definition outDefinition {
    $graphviz$
    $identifier$ ?let blockName
    outDefinition = @graphvizDefinition.new {!blockName}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @graphvizDefinition compute ?!@blockMap blockMap ?let @string inSourceFile {
  message "--------------- Graphviz " + mBlockName + "\n"
  [blockMap searchKey !mBlockName ?let inputVariableList ?let outputVariableList ?let states ?let transitions]
  let variableCount = [inputVariableList length] + [outputVariableList length]
  var graphviz = "digraph G {\n"
  graphviz += "node [shape=record fontname=helvetica style=rounded] ;\n"
#--- Display states result
  let stateList = [states stringValueList !variableCount]
  for (s) in stateList do
    var rs = s # [s reversedString]
    [!?rs insertCharacterAtIndex !':' ![inputVariableList length]]
    graphviz += "\"" + rs + "\" ;\n"
  end
#--- Display transitions
  let transitionList = [transitions stringValueList !variableCount + variableCount]
  for (s) in transitionList do
    var source = [s subString !variableCount !variableCount]
    [!?source insertCharacterAtIndex !':' ![inputVariableList length]]
    var target = [s subString !0 !variableCount]
    [!?target insertCharacterAtIndex !':' ![inputVariableList length]]
    graphviz += "\"" + source + "\" -> \"" + target + "\" ;\n"
  end
  graphviz += "}\n\n"
  let filePath = inSourceFile + "." + mBlockName + ".dot"
  [graphviz writeToFileWhenDifferentContents !filePath ?*]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

