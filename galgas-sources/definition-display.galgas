#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#    AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

refclass @displayDefinition : @definition {
  public var @lstring mBlockName
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#    SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension logicchecker_syntax {

  #·····················································································································

  rule <definition> !@definition outDefinition {
    $display$
    $identifier$ ?let blockName
    outDefinition = @displayDefinition.new {!blockName}
  }

  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

override method @displayDefinition compute
  ?!@blockMap blockMap
  ?let @string unused inSourceFile
{
  print (!"--------------- Display " + self.mBlockName + "\n")
  [blockMap searchKey !self.mBlockName ?let bloc]
  let variableCount = [bloc.mInputVariableList length] + [bloc.mOutputVariableList length]
#--- Display states result
  print (![[bloc.mStates valueCount !variableCount] string] + " states ("
    + [[bloc.mStates nodeCount] string] + " BDD nodes):\n")
  [bloc displayStates !bloc.mStates]
#--- Display transitions
  print (![[bloc.mTransitions valueCount !2 * variableCount] string] + " transitions ("
    + [[bloc.mTransitions nodeCount] string] + " BDD nodes):\n")
  [bloc displayTransitions !bloc.mTransitions]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

