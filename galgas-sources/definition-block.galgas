#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#    AST
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract class @expression {}

#·······················································································································

class @trueExpression : @expression {}

#·······················································································································

class @falseExpression : @expression {}

#·······················································································································

class @varExpression : @expression {
  @lstring mVariableName
}

#·······················································································································

class @negationExpression : @expression {
  @expression mExpression
}

#·······················································································································

class @andExpression, @orExpression, @xorExpression : @expression {
  @expression mLeftExpression
  @expression mRightExpression
}

#·······················································································································

list @assignmentList {
  @bool mLetDeclaration
  @lstring mTargetName
  @expression mExpression
}

#·······················································································································

class @blockDefinition : @definition {
  @lstring mBlockName
  @lstringlist mInputVariableList
  @lstringlist mOutputVariableList
  @assignmentList mAssignmentList
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#    SYNTAX
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

syntax extension logicchecker_syntax {

  #·····················································································································

  rule <definition> !@definition outDefinition {
    $block$
    $identifier$ ?let blockName
    $($
    @lstringlist inputVariableList = {}
    repeat
      $identifier$ ?let inputName
      inputVariableList += !inputName
    while
      $,$
    end
    $)$
    $->$
    $($
    @lstringlist outputVariableList = {}
    repeat
      $identifier$ ?let outputName
      outputVariableList += !outputName
    while
      $,$
    end
    $)$
    ${$
    @assignmentList assignmentList = {}
    repeat
      @bool letDeclaration
      select
        letDeclaration = false
      or
        $let$
        letDeclaration = true
      end
      $identifier$ ?let targetName
      $=$
      <expression> ?let expression
      $;$
      assignmentList += !letDeclaration !targetName !expression
    while
    end
    $}$
    outDefinition = @blockDefinition.new {!blockName !inputVariableList !outputVariableList !assignmentList}
  }

  #·····················································································································

  rule <expression> !@expression outExpression {
    <terme> ?outExpression
    repeat
    while
      $|$
      <terme> ?let expression
      outExpression = @orExpression.new {!outExpression !expression}
    while
      $^$
      <terme> ?let expression
      outExpression = @xorExpression.new {!outExpression !expression}
    end
  }
  
  #·····················································································································

  rule <terme> !@expression outExpression {
    <factor> ?outExpression
    repeat
    while
      $&$
      <factor> ?let expression
      outExpression = @andExpression.new {!outExpression !expression}
    end
  }
  
  #·····················································································································

  rule <factor> !@expression outExpression {
    $/$
    <factor> ?let expression
    outExpression = @negationExpression.new {!expression}
  }
  
  #·····················································································································

  rule <factor> !@expression outExpression {
    $identifier$ ?let variableName
    outExpression = @varExpression.new {!variableName}
  }
  
   #·····················································································································

  rule <factor> !@expression outExpression {
    $($
    <expression> ?outExpression
    $)$
  }
  
  #·····················································································································

  rule <factor> !@expression outExpression {
    $true$
    outExpression = @trueExpression.new
  }
  
  #·····················································································································

  rule <factor> !@expression outExpression {
    $false$
    outExpression = @falseExpression.new
  }
  
  #·····················································································································

}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   SEMANTICS
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

map @variableMap {
  @uint mIndex
  insert insertKey error message "the '%K' variable is already declared in %L"
  search searchKey error message "there is no '%K' variable"
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   STATE COMPUTING
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @expression computeStateExpression ?let @variableMap inVariableMap -> @binaryset

#·······················································································································

override getter @falseExpression computeStateExpression ?let @variableMap unused inVariableMap -> @binaryset {
  result = @binaryset.emptyBinarySet
}

#·······················································································································

override getter @trueExpression computeStateExpression ?let @variableMap unused inVariableMap -> @binaryset {
  result = @binaryset.fullBinarySet
}

#·······················································································································

override getter @negationExpression computeStateExpression ?let @variableMap inVariableMap -> @binaryset {
  result = ~ [mExpression  computeStateExpression !inVariableMap]
}

#·······················································································································

override getter @varExpression computeStateExpression ?let @variableMap inVariableMap -> @binaryset {
  [inVariableMap searchKey !mVariableName ?let varIndex]
  result = @binaryset.binarySetWithBit {!varIndex}
}

#·······················································································································

override getter @andExpression computeStateExpression ?let @variableMap inVariableMap -> @binaryset {
  let left  = [mLeftExpression  computeStateExpression !inVariableMap]
  let right = [mRightExpression computeStateExpression !inVariableMap]
  result = left & right
}

#·······················································································································

override getter @orExpression computeStateExpression ?let @variableMap inVariableMap -> @binaryset {
  let left  = [mLeftExpression  computeStateExpression !inVariableMap]
  let right = [mRightExpression computeStateExpression !inVariableMap]
  result = left | right
}

#·······················································································································

override getter @xorExpression computeStateExpression ?let @variableMap inVariableMap -> @binaryset {
  let left  = [mLeftExpression  computeStateExpression !inVariableMap]
  let right = [mRightExpression computeStateExpression !inVariableMap]
  result = left ^ right
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
#   TRANSITION COMPUTING
#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

abstract getter @expression computeTransitionExpression
  ?let @variableMap unused inVariableMap
  ?let @uint unused inOutputVarCount -> @binaryset

#·······················································································································

override getter @falseExpression computeTransitionExpression 
  ?let @variableMap unused inVariableMap
  ?let @uint unused inOutputVarCount -> @binaryset {
  result = @binaryset.emptyBinarySet
}

#·······················································································································

override getter @trueExpression computeTransitionExpression 
  ?let @variableMap unused inVariableMap
  ?let @uint unused inOutputVarCount -> @binaryset {
  result = @binaryset.fullBinarySet
}

#·······················································································································

override getter @negationExpression computeTransitionExpression
  ?let @variableMap inVariableMap
  ?let @uint inOutputVarCount -> @binaryset
{
  result = ~ [mExpression  computeTransitionExpression !inVariableMap !inOutputVarCount]
}

#·······················································································································

override getter @varExpression computeTransitionExpression 
  ?let @variableMap inVariableMap
  ?let @uint inOutputVarCount -> @binaryset
{
  [inVariableMap searchKey !mVariableName ?let varIndex]
  if varIndex >= inOutputVarCount then # An input variable ?
    result = @binaryset.binarySetWithBit {!varIndex + [inVariableMap count]}
  else
    result = @binaryset.binarySetWithBit {!varIndex}
  end
}

#·······················································································································

override getter @andExpression computeTransitionExpression 
  ?let @variableMap inVariableMap
  ?let @uint inOutputVarCount -> @binaryset
{
  let left  = [mLeftExpression  computeTransitionExpression !inVariableMap !inOutputVarCount]
  let right = [mRightExpression computeTransitionExpression !inVariableMap !inOutputVarCount]
  result = left & right
}

#·······················································································································

override getter @orExpression computeTransitionExpression 
  ?let @variableMap inVariableMap
  ?let @uint inOutputVarCount -> @binaryset
{
  let left  = [mLeftExpression  computeTransitionExpression !inVariableMap !inOutputVarCount]
  let right = [mRightExpression computeTransitionExpression !inVariableMap !inOutputVarCount]
  result = left | right
}

#·······················································································································

override getter @xorExpression computeTransitionExpression 
  ?let @variableMap inVariableMap
  ?let @uint inOutputVarCount -> @binaryset
{
  let left  = [mLeftExpression  computeTransitionExpression !inVariableMap !inOutputVarCount]
  let right = [mRightExpression computeTransitionExpression !inVariableMap !inOutputVarCount]
  result = left ^ right
}

#·······················································································································

override method @blockDefinition compute
  ?!@blockMap blockMap
  ?let @string unused inSourceFile
{
  message "--------------- Block " + mBlockName + "\n"
#---------- Enter output variables
  @variableMap variableMap = {}
  @stringlist outputAndInternalVariableList = {}
  @stringlist outputVariableList = {}
  @variableMap outputVariableMap = {}
  for (name) in mOutputVariableList do
    let variableIndex = [variableMap count]
    [!?variableMap insertKey !name !variableIndex]
    [!?outputVariableMap insertKey !name !variableIndex]
    outputVariableList += !name.string
    outputAndInternalVariableList += !name.string
  end
  let outputVarCount = [outputVariableList length]
#---------- Enter internal variables
  var internalVarCount = 0
  for (letDeclaration name *) in mAssignmentList do
    if letDeclaration then
      internalVarCount ++
      let variableIndex = [variableMap count]
      [!?variableMap insertKey !name !variableIndex]
      [!?outputVariableMap insertKey !name !variableIndex]
      outputAndInternalVariableList += !name.string
    end
  end
  let outputAndInternalVarCount = [outputAndInternalVariableList length]
#---------- Enter input variables
  @stringlist inputVariableList = {}
  for (name) in mInputVariableList do
    [!?variableMap insertKey !name ![variableMap count]]
    inputVariableList += !name.string
  end
  let inputVariableCount = [inputVariableList length]
  let allVariableCount = inputVariableCount + outputAndInternalVarCount
  let exportedVariableCount = inputVariableCount + outputVarCount
#---------- Enter assignments
  @stringset assignedOutputVariableSet = {}
  for (letDeclaration targetVar expression) in mAssignmentList do
    [variableMap searchKey !targetVar ?*]
    if not [outputVariableMap hasKey !targetVar.string] then
      error targetVar : "this variable should be an output variable"
    elsif [assignedOutputVariableSet hasKey !targetVar.string] then
      error targetVar : "output variable has been already assigned"
    end
    assignedOutputVariableSet += !targetVar.string
  end
  for (unassignedVar) in [outputVariableMap keySet] - assignedOutputVariableSet do
    error [outputVariableMap locationForKey !unassignedVar] : "this output variable should be assigned"
  end
#  log variableMap
#---------- Compute states and transitions
  var blockStates = @binaryset.fullBinarySet
  var blockTransitions = @binaryset.emptyBinarySet
  for (* targetVar expression) in mAssignmentList do
  #--- Target var BDD
    [variableMap searchKey !targetVar ?let targetVarIndex]
  #--- Accumulate state
    let stateExpressionBDD = [expression computeStateExpression !variableMap]
    let targetVarBDD = @binaryset.binarySetWithBit {!targetVarIndex}
    blockStates = blockStates & [targetVarBDD equalTo !stateExpressionBDD]
  #--- Accumulate transitions
    let transitionExpressionBDD = [expression computeTransitionExpression !variableMap !outputAndInternalVarCount]
    let shiftedTargetVarBDD = @binaryset.binarySetWithBit {!targetVarIndex + allVariableCount}
    blockTransitions = blockTransitions | [shiftedTargetVarBDD equalTo !transitionExpressionBDD]
  end
#---------- Constraint with stable source states
  blockTransitions = blockTransitions & blockStates
#---------- Constraint with stable target states
  blockTransitions = blockTransitions & (blockStates << allVariableCount)
#---------- remove internal variables
  if internalVarCount > 0 then
    @uintlist stateTranspositionVector = {}
    var idx = 0
    for (*) in mOutputVariableList do
      stateTranspositionVector += !idx
      idx ++
    end
    for (letDeclaration 2*) in mAssignmentList do
      if letDeclaration then
        stateTranspositionVector += !idx + inputVariableCount
        idx ++
      end
    end
    for (*) in mInputVariableList do
      stateTranspositionVector += !idx - internalVarCount
      idx ++
    end
    blockStates = [blockStates transposedBy !stateTranspositionVector]
    blockStates = [blockStates existOnBitIndexAndBeyond !exportedVariableCount]
  #--- Transpose transitions
    @uintlist transTranspositionVector = {}
    idx = 0
    for (*) in mOutputVariableList do
      transTranspositionVector += !idx
      idx ++
    end
    for (letDeclaration 2*) in mAssignmentList do
      if letDeclaration then
        transTranspositionVector += !idx + exportedVariableCount + inputVariableCount
        idx ++
      end
    end
    for (*) in mInputVariableList do
      transTranspositionVector += !idx - internalVarCount
      idx ++
    end
    for (*) in mOutputVariableList do
      transTranspositionVector += !idx - internalVarCount
      idx ++
    end
    for (letDeclaration 2*) in mAssignmentList do
      if letDeclaration then
        transTranspositionVector += !idx + inputVariableCount
        idx ++
      end
    end
    for (*) in mInputVariableList do
      transTranspositionVector += !idx - 2 * internalVarCount
      idx ++
    end
    # log transTranspositionVector
    blockTransitions = [blockTransitions transposedBy !transTranspositionVector]
    blockTransitions = [blockTransitions existOnBitIndexAndBeyond !2 * exportedVariableCount]
  end
#---------- Display states result
  message [[blockStates valueCount !exportedVariableCount] string]
      + " states (" + [[blockStates nodeCount] string] + " BDD nodes)\n"
#---------- Display transitions
  message [[blockTransitions valueCount !2* exportedVariableCount] string] + " transitions (" +
     [[blockTransitions nodeCount] string] + " BDD nodes)\n"
#---------- Enter in block map
   let b = @block.new {!inputVariableList !outputVariableList !blockStates !blockTransitions}
  [!?blockMap insertKey !mBlockName !b]
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

